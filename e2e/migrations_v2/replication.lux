[doc test flow of migrations from pg -> electric -> replication stream]

[global dprompt=\w+!?@\w+:(\S+)[\#\$]]
[global eprompt=iex\(\d+\)>\s]
[global psql=electric]
[global user_id_1=1]

[include shared.luxinc]
[invoke setup]

[macro client_session user_id session_id]
    [invoke log "user ${user_id}: session ${session_id}: start"]
    [local client_id=client_${user_id}_${session_id}]
    !provider = {Electric.Satellite.Auth.JWT, issuer: "dev.electric-sql.com", \
                    secret_key: "integration-tests-signing-key-example"}
    ?$eprompt
    !{:ok, conn} = Electric.Test.SatelliteWsClient.connect_and_spawn( \
            [ auth: %{auth_provider: provider, user_id: "$user_id"}, \
                id: "$client_id", \
                debug: true, \
                sub: "0", \
                auto_in_sub: true, \
                format: :term, \
                host: "electric_1", \
                auto_ping: :true \
            ])
    ?+$eprompt
    ?(.*) %(.*).SatInStartReplicationReq{(.*)lsn: "", (.*)
    ?(.*) %(.*).SatInStartReplicationResp{(.*)

[endmacro]

[shell pg_1]
    [invoke log "run migration on postgres"]
    !BEGIN;
    ?electric=\*#
    # need to invoke the migration id function
    !CREATE TABLE mtable1 (id uuid PRIMARY KEY);
    ?electric=\*#
    !COMMIT;
    ?electric=#

[shell user_1_ws1]
    [invoke start_elixir_test 1]
    [invoke client_session $user_id_1 1]

[shell electric]
    ?.* origin=postgres_2 .* \[debug\] Sending \d messages to the subscriber
    ?"query" => "CREATE TABLE mtable1 \(id uuid PRIMARY KEY\);",
    ?relation: {"electric", "ddl_commands"}

[shell user_1_ws1]
    ?.*SatOpMigrate\{.*sql: "CREATE TABLE \\"mtable1\\".*

[cleanup]
    [invoke teardown]
